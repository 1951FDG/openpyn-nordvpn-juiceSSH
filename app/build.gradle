plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.android.gms.strict-version-matcher-plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.getkeepsafe.dexcount'
    id 'com.github.konifar.gradle.unused-resources-remover'
    id 'com.gladed.androidgitversion'
    id 'io.gitlab.arturbosch.detekt'
    id 'smartdengg.clickdebounce'
    //id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    ndkVersion '21.4.7075529'

    defaultConfig {
        applicationId 'io.github.getsixtyfour.openpyn'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resConfigs 'en'
    }

    buildTypes {
        release {
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-release.pro'
        }
        debug {
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildFeatures {
        aidl false
        dataBinding false
        renderScript false
        shaders false
        viewBinding true
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    sourceSets {
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
    }

    aaptOptions {
        noCompress 'mbtiles'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3'
        languageVersion = '1.3'
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }

    lintOptions {
        abortOnError false
        baseline file('lint-baseline.xml')
        //check 'ContentDescription', 'ConvertToWebp', 'DuplicateStrings', 'RtlHardcoded', 'SyntheticAccessor', 'UnusedIds', 'UnusedResources'
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning', 'VectorPath', 'InvalidSingleLineComment', 'RawDimen', 'ShouldUseStaticImport', 'XmlSpacing'
        enable 'WrongThreadInterprocedural'
        xmlReport false
    }

    testOptions {
        animationsDisabled true
    }
}

androidGitVersion {
    format '%describe%%-branch%%-dirty%'
}

debounce {
    loggable = true
    exclusion = [
            'androidx/preference/Preference$1'                                    : ['onClick(Landroid/view/View;)V'],
            'com/abdeveloper/library/MultiSelectViewHolder'                       : ['onClick(Landroid/view/View;)V'],
            'com/google/android/gms/common/SignInButton'                          : ['onClick(Landroid/view/View;)V'],
            'com/google/android/gms/dynamic/zae'                                  : ['onClick(Landroid/view/View;)V'],
            'io/github/getsixtyfour/openpyn/MainActivity'                         : ['onClick(Landroid/view/View;)V'],
            'io/github/getsixtyfour/openpyn/core/MapViews$OnFavoriteClickListener': ['onClick(Landroid/view/View;)V'],
    ]
}

detekt {
    toolVersion = '1.5.1'
    input = files('src/main/kotlin')
    baseline = file('detekt-baseline.xml')
    config = files("$rootDir/config/detekt/detekt.yml")
    reports {
        xml {
            enabled = false
            destination = file("$buildDir/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("$buildDir/reports/detekt/detekt.html")
        }
    }
}

dexcount {
    enabled = false
}

configurations {
    implementation {
        /*exclude group: 'org.jetbrains.kotlinx', module: 'atomicfu'*/
    }

    /*debugAndroidTestCompileClasspath {
        exclude group: 'androidx.annotation'
        exclude group: 'androidx.appcompat'
        exclude group: 'androidx.collection'
        exclude group: 'androidx.core'
        exclude group: 'androidx.fragment'
        exclude group: 'androidx.lifecycle'
        exclude group: 'androidx.recyclerview'
        exclude group: 'androidx.savedstate'
        exclude group: 'androidx.transition'
        exclude group: 'com.google.firebase', module: 'firebase-common'

        resolutionStrategy {
            failOnVersionConflict()
        }
    }*/
}

afterEvaluate {
    // Android Gradle Plugin 4.1.0 breaks build config field injecting in variants dynamically
    // https://issuetracker.google.com/issues/171898346
    /*generateDebugResValues.dependsOn(appGenerateApiBuildConstants)
    generateDebugBuildConfig.dependsOn(appGenerateGitBuildConstants)
    generateReleaseResValues.dependsOn(appGenerateApiBuildConstants)
    generateReleaseBuildConfig.dependsOn(appGenerateGitBuildConstants)*/

    assembleRelease.dependsOn appCheckIfDirty, appCheckIfSame, appCheckIfTagged
}

dependencies {
    // Java 8+ libraries
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Modules
    implementation project(':country-flags-vpn')
    implementation project(':multi-select-dialog')
    implementation project(':openvpn-service')
    implementation project(':sqlite-ndk')

    // 1951FDG
    implementation 'com.github.1951FDG:ProgressToolbar:1.0.9'
    implementation 'com.github.1951FDG:juicessh-pluginlibrary:1.0.18'
    //noinspection GradleDependency
    debugImplementation 'com.github.1951FDG.AndroidPerformanceMonitor:blockcanary-android:slave-SNAPSHOT'

    // Android
    implementation "androidx.appcompat:appcompat:${androidXVersions.appcompat}"
    implementation "androidx.constraintlayout:constraintlayout:${androidXVersions.constraintlayout}"
    implementation "androidx.coordinatorlayout:coordinatorlayout:${androidXVersions.coordinatorlayout}"
    implementation "androidx.core:core-ktx:${androidXVersions.core}"
    implementation "androidx.navigation:navigation-fragment-ktx:${androidXVersions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${androidXVersions.navigation}"
    implementation "androidx.preference:preference:${androidXVersions.preference}"
    implementation "androidx.recyclerview:recyclerview:${androidXVersions.recyclerview}"
    implementation "androidx.sqlite:sqlite:${androidXVersions.sqlite}"

    // Architecture
    implementation('com.naver.android.svc:svc:1.0.1') {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-extensions'
    }
    //kapt 'com.naver.android.svc:svc-compiler:1.0.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1-native-mt'

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:18.1.0'

    // Error Prone
    //debugAnnotationProcessor 'com.uber.nullaway:nullaway:0.8.0'
    //debugCompileOnly 'com.jakewharton.nopen:nopen-annotations:1.0.1'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.play:core:1.10.0'

    // Google Material Design
    implementation "com.google.android.material:material:$mdc_version"

    // HTTP Client
    // v1.4.0 : HTTP Client tracing using Stetho Android library !
    implementation('io.ktor:ktor-client-android:1.3.2') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core-common'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    // JSON
    implementation 'com.squareup.moshi:moshi:1.12.0'

    // Kotlin
    implementation('org.jetbrains.kotlin:kotlin-stdlib') {
        version {
            strictly "$kotlin_version"
        }
    }
    implementation('org.jetbrains.kotlin:kotlin-stdlib-common') {
        version {
            strictly "$kotlin_version"
        }
    }
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk7') {
        version {
            strictly "$kotlin_version"
        }
    }
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8') {
        version {
            strictly "$kotlin_version"
        }
    }

    // LeakCanary
    implementation 'com.squareup.leakcanary:plumber-android:2.7'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // Lint
    debugImplementation 'com.vanniktech:lint-rules-android:0.14.0'

    // Logger
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'io.github.microutils:kotlin-logging:1.12.5'
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    debugImplementation 'at.favre.lib:slf4j-timber:1.0.1'
    debugImplementation('com.github.AppDevNext.Logcat:LogcatCore:2.8.0') {
        transitive = false
    }
    //debugImplementation(name:'LogcatCore-debug', ext:'aar')

    // Stetho
    debugImplementation('com.facebook.stetho:stetho-timber:1.6.0') {
        exclude group: 'com.jakewharton.timber'
    }

    // Test
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test:core-ktx:${testRunnerVersion}"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:${testRunnerVersion}"
    androidTestImplementation "androidx.test:rules:${testRunnerVersion}"

    // Espresso dependencies
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"

    androidTestImplementation 'com.schibsted.spain:barista:3.9.0'

    // Screengrab
    androidTestImplementation 'com.jraska:falcon:2.2.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.0'
}

apply from: 'other.gradle'
apply from: 'inject.gradle'
