import com.google.firebase.crashlytics.buildtools.gradle.tasks.UploadMappingFileTask
import org.ajoberstar.grgit.Grgit

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://jitpack.io' }
    }

    ext {
        androidXVersions = [
                appcompat        : '1.3.0',
                constraintlayout : '2.1.0-beta02',
                coordinatorlayout: '1.1.0',
                core             : '1.6.0',
                navigation       : '2.3.5',
                preference       : '1.1.1',
                recyclerview     : '1.2.1',
                sqlite           : '2.1.0',
        ]
        annotations_version = '20.1.0'
        espressoVersion = '3.4.0'
        junit_version = '4.13.2'
        kotlin_gradle_version = '1.4.32'
        kotlin_version = '1.3.72'
        mdc_version = '1.3.0'
        slf4j_version = '1.7.30'
        testRunnerVersion = '1.4.0'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_gradle_version"

        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${androidXVersions.navigation}"

        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'

        classpath 'com.google.android.gms:strict-version-matcher-plugin:1.2.2'

        // firebase-crashlytics-gradle:2.7.0,2.7.1 is broken
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.6.1'

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:2.0.0'

        classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'

        classpath 'com.github.SmartDengg.asm-clickdebounce:click-debounce-gradle-plugin:2.1.0'

        classpath 'gradle.plugin.com.github.konifar.gradle:plugin:0.3.3'

        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.4'

        classpath 'com.gladed.androidgitversion:gradle-android-git-version:0.4.14'

        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.17.1'

        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:2.0.1'

        //classpath 'com.google.gms:google-services:4.3.4'

        // NOTE: Use app/src/release/res/values/values.xml instead of google-services plugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'idea'
}

idea.module {
    excludeDirs += file('config/detekt')
    excludeDirs += file('config/checkstyle')
    excludeDirs += file('config/lib')

    excludeDirs += file('fastlane')

    excludeDirs += file('node_modules')
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: 'net.ltgt.errorprone'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter() {
            content {
                includeGroup('com.schibsted.spain')
                includeGroup('io.ktor')
                includeModule('org.jetbrains.kotlinx', 'kotlinx-html-jvm')
            }
        }
        flatDir {
            dirs 'libs'
        }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
        errorprone 'com.google.errorprone:error_prone_core:2.7.1'
        errorprone 'com.jakewharton.nopen:nopen-checker:1.0.1'
        errorprone 'com.uber.nullaway:nullaway:0.9.1'
    }

    gitProperties {
        extProperty = 'gitProps'
        failOnNoGitDirectory = false
        gitPropertiesDir = "$project.rootDir"
        keys = ['git.branch', 'git.closest.tag.commit.count', 'git.commit.id', 'git.dirty', 'git.remote.origin.url']

        customProperty 'git.total.commit.ahead.count', { Grgit grgit ->
            grgit.log { range('origin/slave', 'slave') }.size()
        }

        customProperty 'git.total.commit.behind.count', { Grgit grgit ->
            grgit.log { range('slave', 'origin/slave') }.size()
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).configureEach {
            //options.compilerArgs << '-Xlint:all'
            options.deprecation = true
            options.failOnError = true
            options.errorprone {
                //allDisabledChecksAsWarnings = true
                disableAllChecks = true
                disable('AlmostJavadoc')
                disable('AndroidJdkLibsChecker')
                disable('BooleanParameter')
                disable('EmptyCatch')
                disable('Java7ApiChecker')
                disable('MissingSummary')
                //warn('Nopen')
                //warn('NullAway')
                disable('StringSplitter')
                disable('UnnecessaryDefaultInEnumSwitch')
                disable('Var')
                option('NullAway:AnnotatedPackages', 'com.getsixtyfour,io.github.getsixtyfour')
                excludedPaths = '.*/app/build/.*|.*/almworks/.*|.*/requery/.*|.*/javolution/.*|.*/sqlite/.*'
            }
        }

        tasks.withType(UploadMappingFileTask).configureEach {
            dependsOn(appCopyGoogleIdValuesTask)
        }

        tasks.named('dependencyUpdates').configure {
            resolutionStrategy {
                componentSelection {
                    all {
                        if (candidate.version.contains('-alpha')) {
                            reject()
                        }
                        if (candidate.version.contains('-beta')) {
                            reject()
                        }
                        if (candidate.group.contains('org.jetbrains.kotlin')) {
                            reject()
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
